SHELL    TITLE 'PROGRAM SHELL'
SHELL    CSECT
SHELL    AMODE 31
SHELL    RMODE ANY
         SAVE  (14,12),,'SHELL V01 &SYSDATC &SYSTIME'
         LR    R12,R15                 Set up Base Reg (GR and AR)
         USING SHELL,R12
         LR    R2,R1                   Save the Parameter Register
                                       SPACE 1
         L     R0,=A(GSLENG)           Storage Length
         STORAGE OBTAIN,LENGTH=(0),LOC=BELOW,BNDRY=PAGE
         ST    R13,4(,R1)              Back Pointer
         ST    R1,8(,R13)              Forward pointer
         LR    R13,R1                  Address the work Area
         USING GS,R13
         LA    R1,DB                   Clear obtained storage
         LR    R0,R1                   Clear obtained storage
         L     R1,=A(CLRLENG)          Clear obtained storage
         SR    R15,R15                 Clear obtained storage
         MVCL  R0,R14                  Clear obtained storage
                                       SPACE 1
         DEBUG DATA=G,TYPE=PUT,MAXLEN=200,OPEN=YES,                    *
               PGMBASE=R12,USING=(GS,R13)
         DEBUG ID=BEGIN,(SHELL+5,28,C)
                                       SPACE 1
*---------------------------------------------------------------------*
*        Program Initialisation                                       *
*---------------------------------------------------------------------*
                                       SPACE 1
         L     R9,0(R2)                Address the Parm
         DEBUG ID=BEGIN,(PARML,2,X),(PARMC,24,C),USING2=(PARM,R9)
                                       SPACE 1
         L     R8,CVTPTR               A(CVT)
         USING CVTMAP,R8
         MVC   SYSTEMNM,CVTSNAME       Save the System Name
         DEBUG ID=BEGIN,(SYSTEMNM,8,C)
         CLC   =CL8'NOR2',CVTSNAME     Is this NOR2
         BE    ABEND0C1                ---> Yes, abend
                                       SPACE 1
*---------------------------------------------------------------------*
*        Program Termination                                          *
*---------------------------------------------------------------------*
                                       SPACE 1
         DEBUG DATA=G,CLOSE=YES
         L     R0,=A(GSLENG)           Obtained Storage Length
         LR    R1,R13                  Obtained Storage Address
         L     R2,4(R13)               Callers Save Area
         STORAGE RELEASE,LENGTH=(0),ADDR=(1)
         LR    R13,R2                  Restore Callers Save Area
         SR    R15,R15                 Set the Return Code
         RETURN (14,12),RC=(15)        ===> Return to Caller
                                       SPACE 1
*---------------------------------------------------------------------*
*        Constants and Literals                                       *
*---------------------------------------------------------------------*
                                       SPACE 1
ABEND0C1 DC    X'0000'                 Force an abend if executed
         LTORG ,
         DEBUG DATA=C                  Debug Constants
                                       SPACE 1
*---------------------------------------------------------------------*
*        Working Storage                                              *
*---------------------------------------------------------------------*
                                       SPACE 1
GS       DSECT
SAVEAREA DS    18F                     Register Save Area
DB       DS    D                       Work Area
SYSTEMNM DS    CL8                     Work Area
         DEBUG DATA=V                  Debug Variables
         CNOP  0,8                     Terminate on a Double Word Bdy.
CLRLENG  EQU   *-DB
GSLENG   EQU   *-GS                    Length of obtained storage
                                       SPACE 1
*---------------------------------------------------------------------*
*        DSECTs                                                       *
*---------------------------------------------------------------------*
                                       SPACE 1
PARM     DSECT
PARML    DS    XL2                     Parameter Length
PARMC    DS    0C                      Parameter Value
                                       SPACE 1
         YREGS
         CVT   DSECT=YES,PRINT=NO
         END
